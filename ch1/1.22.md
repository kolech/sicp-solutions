Question
--------
Most Lisp implementations include a primitive called `runtime` that returns an
integer that specifies the amount of time the system has been running
(measured, for example, in microseconds). The following `timed-prime-test`
procedure, when called with an integer $n$, prints $n$ and checks to see if $n$
is prime. If $n$ is prime, the procedure prints three asterisks followed by the
amount of time used in performing the test

```scheme
(define (timed-prime-test n)
        (newline)
        (display n)
        (start-prime-test n (runtime)))
        
(define (start-prime-test n start-time)
    (if (prime? n)
        (report-prime (- (runtime) start-time))))

(define (report-prime elapsed-time)
        (display " *** ")
        (display elapsed-time))
```

Using this procedure, write a procedure `search-for-primes` that checks the
primality of consecutive odd integers in a specified range. Use your procedure
to find the three smallest primes larger than 1000; larger than 10,000; larger
than 100,000: larger than 1,000,000. Note the time needed to test each prime.
Since the testing algorithm has order of growth of $\Theta (\sqrt n)$, you
should expect that testing for primes around 10,000 should take about $\sqrt{
10}$ times as long as testing for primes around 1000. Do your timing data bear
this out? How well do the data for 100,000 and 1,000,000 support the $\Theta
(\sqrt n)$ prediction? Is your result compatible with the notion that programs
on your machine run in time proportional to the number of steps required for
the computation?

Solution
--------
The procedure `search-for-primes` can be written as follows:

```scheme
(define (search-for-primes lower upper)  
  (define (search-primes a)
    (cond((< a upper) (timed-prime-test a)
                      (search-primes (+ a 2)))))
  
  (search-primes (next-odd lower)))

(define (next-odd n)
    (if (= (remainder n 2) 0) (+ n 1) n))

(define (timed-prime-test n)
        (newline)
        (display n)
        (start-prime-test n (runtime)))
        
(define (start-prime-test n start-time)
    (if (prime? n)
        (report-prime (- (runtime) start-time))))

(define (report-prime elapsed-time)
        (display " *** ")
        (display elapsed-time))

(define (prime? n) (= (smallest-divisor n) n))

(define (smallest-divisor n) (find-divisor n 2))

(define (find-divisor n test-divisor)
    (cond ((> (square test-divisor) n) n)
          ((divides? test-divisor n) test-divisor)
          (else (find-divisor n (+ test-divisor 1)))))

(define (square n) (* n n))

(define (divides? a b) (= (remainder b a) 0))
```

The twelves primes from the for ranges are: 1009 1013 1019 10007 10009 10037
100003 100019 100043 1000003 1000033 1000037

The following table shows the average run time of the procedure in each 
of the four ranges

$$\begin{array} {|r|r|r|}
    \hline n & run\ time & average\ steps \\
    \hline 1000 & 2.7 & 32 \\
    \hline 10000 & 6.36  & 100 \\
    \hline 100000 & 17.68  & 316 \\
    \hline 1000000 & 54.48 & 1000 \\
    \hline
\end{array}$$

Note that the run time for each input is computed as the average of the middle
50% of 1000 invocation of the procedure.

We see that as the input size increases by an order of magnitude, so does the
average run time by a value closer to $\sqrt 10$
