Question
--------
a. The `sum` procedure is only the simplest of a vast number of similar
abstractions that can be captured as higher-order procedures. Write an
analogous procedure called `product` that returns the product of the values of
a function at points over a given range. Show how to define `factorial` in
terms of product. Also use `product` to compute approximations of $\pi$ using
the formula

$$ {\pi \over 4} = {2.4.4.6.6.8... \over 3.3.5.5.7.7...} $$

b. If your `product` procedure generates a recursivce process, write one that
generates an iterative process.  if it generates an iterative process, write
one that generates a recursive process.

Solution
--------
Let us begin by defining the `product` procedure. The pattern of performing a
product of the terms in a sequence is identical to performing the sum over the
terms. Therefore, by changing what gets returned and the binary arithmetic
operator used to combine the terms, the pattern of `sum` can be used as it is
as follows:

```scheme
; product procedure that generates recursive process
(define (product term a next b)
  (if (> a b)
      1
      (* (term a)
         (product term (next a) next b))))

; product procedure that generates iterative process
(define (prodcut term a next b)
  (define (iter a result)
    (if (> a b)
        result
        (iter (next a) (* result (term a)))))
  (iter a 1))
```

Now the `factorial` procedure can be written as follows
```scheme
(define (factorial n)
  (define (identity x) x)

  (define (inc x) (+ x 1))

  (product identity 1 inc n))
```

Now let us get back to writting the a procedure to calculate $\pi$.
Rearranging the formula as follows can give us a better idea on how to apply
the product 

$$
\pi = 4.2 {4.4.6.6.8... \over 3.3.5.5.7.7...}
    = 8 {4^2.6^2.8... \over 3^2.5^2.7^2...}
    = 8 .8 {4^2.6^2... \over 3^2.5^2.7^2...}
$$

This can be further generalized in terms of the number of square terms in the
numerator

$$
    f(n) = 8(2n+2) {{\prod_{i=1}^n (2i+2)^2} \over {\prod_{i=1}^{n+1} (2i+1)^2}}
$$

```scheme
(define (pi-prod n)
  (define (square x) (* x x))

  (define (next x) (+ x 2))

  (define (numerator)
    (product square 4 next (+ (* 2 n) 2)))

  (define (denominator)
    (product square 3 next (+ (* 2 n) 3)))
  
  (*
   8.0
   (+ (* 2 n) 2)
   (/ (numerator) (denominator))))
```
