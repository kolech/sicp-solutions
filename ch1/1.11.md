Question
========
A function $f$ is defined by the rule that

$${
    f(n) = 
    \begin{cases}
    n, & \text{if n < 3} \\
    f(n-1) + 2f(n-2) + 3f(n-3), & \text {if n >= 3}
    \end{cases}
}$$ 

Write a procedure that computes $f$ by means of a recursive process. Write a
procedure that computes $f$ by means of an iterative process.

Solution
========

Recursive Process
-----------------
```scheme
(define (f n)
    (if (< n 3)
        n
        (+ (f (- n 1))
           (* 2 (f (- n 2)))
           (* 3 (f (- n 3))))))
```

Iterative Process
-----------------
A procedure that generates an iterative process can be defined following the
same logic laid out in Section 1.2.2 for Fibonacci numbers. Here we can make
use of three integers $a$, $b$ and $c$, initialized to $f(2) = 2$,
$f(1) = 1$ and $f(0) = 0$, and repeatedly applying the simultaneous
transformations:

$\qquad a \leftarrow a + 2b + 3c$ \
$\qquad b \leftarrow a$ \
$\qquad c \leftarrow b$

After $n$ iterations, $a$ holds $f(n+2)$, $b$ holds $f(n+1)$ and
$c$ holds $f(n)$


```scheme
(define (f n)
    (define (f-iter a b c count)
            (if (= 0 count)
                c
                (f-iter (+ a (* 2 b) (* 3 c))
                        a
                        b
                        (- count 1))))
    (f-iter 2 1 0 n))
```



