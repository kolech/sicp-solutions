Question
========
The following procedure computes a mathematical function called Ackermann's
function.
```scheme
(define (A x y)
    (cond ((= y 0) 0)
          ((= x 0) (* 2 y))
          ((= y 1) 2)
          (else (A (- x 1) (A x (- y 1))))))
```

What are the values of the following expressions?

```scheme
(A 1 10)
(A 2 4)
(A 3 3)
```

Consider the following procedures, where _A_ is the procedure defined above:

```scheme
(define (f n) (A 0 n))
(define (g n) (A 1 n))
(define (h n) (A 2 n))
(define (k n) (* 5 n n))
```

Give precise mathematical definitions for the functions computed by the
procedures `f`, `g`, and `h` for positive integer values of _n_. For example,
`(k n)` computes $5n^2$.

Solution
========

Evalution of the expressions
----------------------------
```scheme
(A 1 10)
```
_1024_

```scheme
(A 2 4)
```
_65536_

```scheme
(A 3 3)
```
_65536_


Derivation of _f(n)_
--------------------

The result of the of evaluation of `(A 0 n)` is readily available from the
definition of the procedure `A`, i.e, it evaluates to the second base case of
the procedure. Therefore, `(A 0 n)` evaluates to `2n`. Thus $${f(n) = 2n}$$.


Derivation of _g(n)_
--------------------

By using the substitution model, we can visualize how the expression `(A 1 n)`
expands:
```scheme
(A 1 n)
(A 0 (A 1 (- n 1)))
(A 0 (A 0 (A 1 (- n 2))))
.
.
.
(A 0 (A 0 (... A(0 (A 1 1)))))
(A 0 (A 0 (... A(0 2))))
``` 

Now what is left is the evaluation of the _n-1_ deferred expressions of the form
`(A 0 y)`. By recursively applying the second case of the procedure `A`, the
expression `(A 0 (A 0 (... A(0 2))))` evaluates to $2^n$. Therefore `g(n)` is
defined as: 

$${g(n) = 2^n}$$


Derivation of `h(n)`
--------------------
Let's visualize how the expression `(A 2 n)` expands:

```scheme
(A 2 n)
(A 1 (A 1 (- n 1)))
(A 1 (A 1 (A 1 (- n 2))))
.
.
.
(A 1 (A 1 (... A(1 (A 1 1)))))
``` 

Now we have only _n_ deferred expressions of the form `(A 1 y)`. Therefore,
substituting $`2^i`$ for `(A 1 i)`, the expression `(A 1 (A 1 (... A(1 (A 1 1)))))`
results in a repeated exponentiation of the form ${2^{2^{.^{.^{^{2}}}}}}$.
Such an expression is called _tetration_ and denoted by ${a \uparrow \uparrow n}$
and defined as:

$$
    {a \uparrow \uparrow n} =
    \begin{cases}
    1, & \text{if n = 0} \\
    {a^{a \uparrow \uparrow n-1}}, & \text {if n > 0}
    \end{cases}
$$ 

With this definition in place, `h(n)` is defined as:
$${h(n) = {2 \uparrow \uparrow n}}$$

Note that as per the requirement of the exercise, we are looking for a definition
for positive integers. Thus `(A 2 n)` == ${2 \uparrow \uparrow n}$ only for
${n > 0}$.
