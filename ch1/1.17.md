Question
--------
The exponentiation algorithms in Section 1.2.4 are based on performing
exponentiation by means of repeated multiplication. In a similar way, one can
perform integer multiplication by means of repeated addition. The following
multiplication procedure (in which it is assumed that our language can only
add, not multiply) is analogous to the `expt` procedure:

```scheme
(define (* a b)
    (if (= b 0)
        0
        (+ a (* a (- b 1)))))
```

This algorithm takes a number of steps that is linear in $b$. Now suppose we
include, together with addition, operations `double`, which doubles an integer,
and `halve`, which divides an (even) integer by 2. Using these, design a
multiplication procedure analogous to `fast-expt` that uses a logarithmic
number of steps.

Solution
--------
A similar reasoning used to come up with `fast-expt-iter` can be applied also
here by taking into consideration the invariant quantity $p + a*b$ at each
iteration, where $p$ is zero when the computation starts and holds the final
product at the end of the computation.

The sum $p + a*b$ is unchanged if the following transformation is applied on
$p$ at each iteration:

$\qquad p \leftarrow p      \ \ \ \qquad     \text{if n is even}$, \
$\qquad p \leftarrow p + a  \quad     \text{if n is odd}$

The following mathematical definition captures how the proposed algorithm
computes the product of $a$ and $b$:

$$
    f(a, b, p) = 
    \begin{cases}
    p, & \text{if b = 0} \\
    f(2a, b/2, p), & \text{if b is even} \\
    f(a, b-1, p+a), & \text{if b is odd} \\
   \end{cases}
$$

Now the multiplication $a*b$ is computed by evaluating $f(a, b, 0)$. And this
is exactly what is done in the procedure `fast-mult-iter`.

```scheme
(define (fast-mult a b)
    (fast-mult-iter a b 0))

(define (fast-mult-iter a b p)
    (cond ((= b 0) p)
          ((even? b) (fast-mult-iter (double a) (halve b) p))
          (else (fast-mult-iter a (- b 1) (+ p a)))))

(define (even? n) (= (remainder n 2) 0))

(define (double n) (+ n n))

(define (halve n) (/ n 2))
```