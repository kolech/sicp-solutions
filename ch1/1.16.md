Question
--------
Design a procedure that evolves an iterative exponentiation process that
uses successive squaring and uses a logarithmic number of steps as does
`fast-expt`. (Hint: Using the observation that $(b^{n/2})^2 = (b^2)^{n/2}$,
keep, along with the exponent $n$ and the base $b$, an additional state
variable $a$, and define the state transformation in such a way that the
product $ab^n$ is unchanged from state to state. At the beginning of the
process $a$ is taken to be 1, and the answer is given by the value of $a$ at
the end of the process. In general, the technique of defining an _invariant
quantity_ that remains unchanged from state to state is a powerful way to think
about the design of iterative algorithms.

Solution
--------
Below is the definition of exponentiation that uses successive squaring:

$\qquad b^n = (b^2)^{n/2} \qquad    \text{if n is even}$ \
$\qquad b^n = b(b^{n - 1}) \qquad    \text{if n is odd}$

The product $ab^n$ is unchanged if the following transformation is applied on
$a$ at each iteration:

$\qquad a \leftarrow a \qquad     \text{if n is even}$, \
$\qquad a \leftarrow ab  \qquad     \text{if n is odd}$

Basically this transformation accumulates $b$, by means of multiplication, when
$n$ is odd.

With this transformation in place, `fast-expt` can be redefined in terms of
`fast-expt-iter` as follows:

```scheme
(define (fast-expt-iter b n a)
    (cond ((= n 0) a)
          ((even? n) (fast-expt-iter (square b) (/ n 2) a))
          (else (fast-expt-iter b (- n 1) (* b a)))))

(define (even? n)(= (remainder n 2) 0))

(define (square n) (* n n))

(define (fast-expt b n)
    (fast-expt-iter b n 1))
```
