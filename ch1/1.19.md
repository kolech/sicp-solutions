Question
--------
There is a clever algorithm for computing the Fibonacci numbers in a
logarithmic number of steps. Recall the transformation of the state variables
$a$ and $b$ in the `fib-iter` process of Section 1.2.2: $a \leftarrow a+b$ and
$b \leftarrow a$. Call this transformation _T_, and observe that applying _T_
over and over again $n$ times, starting with 1 and 0, produces the pair
Fib(n + 1) and Fib(n).  In other words, the Fibonacci numbers are produced by
applying $T^n$, the $n^{th}$ power of the transformation _T_, starting with
the pair (1, 0). Now consider _T_ to be the special case of $p = 0$ and
$q = 1$ in a family of transformations $T_{pq}$, where $T_{pq}$ transforms the
pair (a, b) according to $a \leftarrow bq+aq + ap$ and $b \leftarrow bp + aq$.
Show that if we apply such a transformation $T_{pq}$ twice, the effect is the
same as using a single transformation $T_{pq}$ of the same form, and compute
$p \prime$ and $q \prime$ in terms of $p$ and $q$. This gives us an explicit
way to square these transformations, and thus we can compute $T^n$ using
successive squaring, as in the `fast-expt` procedure. Put this all together
to complete the following procedure, which runs in a logarithmic number of
steps.

```scheme
(define (fib n)
    (fib-iter 1 0 0 1 n))
(define (fib-iter a b p q count)
    (cond ((= count 0) b)
          ((even? count)
           (fib-iter a
                     b
                     (??)   ; compute p'
                     (??)   ; compute q'
                     (/ count 2)))
           (else (fib-iter (+ (* b q) (* a q) (* a p))
                           (+ (* b p) (* a q))
                           p
                           q
                           (- count 1)))))

```

Solution
--------

Applying $T_{pq}$ on $a$ and $b$ the first time transforms them to
$bq + aq + ap$ and $bp + aq$, respectively. A second application of the
transformation results in $b(2pq + q^2) + a(2pq + q^2) + a(p^2 + q^2)$ and
$b(p^2 + q^2) + a(2pq + q^2)$, respectively. From this we get,
$p\prime$ and $q\prime$ as follows:

$\qquad p \prime = p^2 + q^2$ \
$\qquad q \prime = 2pq + q^2$

Now the complete procedure is defined as follows:

```scheme
(define (fib n)
    (fib-iter 1 0 0 1 n))
(define (fib-iter a b p q count)
    (cond ((= count 0) b)
          ((even? count)
           (fib-iter a
                     b
                     (+ (square p) (square q))
                     (+ (* 2 p q) (square q))
                     (/ count 2)))
           (else (fib-iter (+ (* b q) (* a q) (* a p))
                           (+ (* b p) (* a q))
                           p
                           q
                           (- count 1)))))

(define (even? n)
    (= (remainder n 2) 0))

(define (square n)
    (* n n))
```
