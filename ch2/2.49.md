Question
---
Use `segments->painter` to define the following primitive painters:

1. The painter that draws the outline of the designated frame. 
2. The painter that draws an "X" by connecting opposite
corners of the frame.
3. The painter that draws a diamond shape by connecting
the midpoints of the sides of the frame.
4. The wave painter.

Solution
---
For completeness here is the definitions of `segments->painter`, and `frame-coord-map`

```scheme
(define (frame-coord-map frame)
  (lambda (v)
    (add-vect
     (origin-frame frame)
     (add-vect (scale-vect (xcor-vect v) (edge1-frame frame))
               (scale-vect (ycor-vect v) (edge2-frame frame))))))

(define (segments->painter segments-list)
  (lambda (frame)
    (for-each (lambda (segment) (draw-line ((frame-coord-map frame) (start-segment segment))
                                           ((frame-coord-map frame) (end-segment segment))))
              segments-list)))
```

Except for point 4, the problem boils down to finding the list of line segments that make up the desired image.

1. The outline of a frame is defined by the list of line segments that connect the corners. Let $bl$, $tl$, $tr$, and $br$ be the vectors that represent the bottom-left, top-left, top-right, and bottom-right corners of the frame, respectively, which, in turn, are expressed in unit square as (0 0), (0 1), (1 1), and (1 0), respectively; thus, drawing the outline can be defined as drawing the line segments `(list (bl tl) (tl tr)(br tr) (bl br))`:

```scheme
(define frame-outline
  (lambda (frame)
    (let ((bl (make-vect 0 0))
          (tl (make-vect 0 1))
          (br (make-vect 1 0))
          (tr (make-vect 1 1)))
      (let ((left-segment (make-segment bl tl))
            (top-segment (make-segment tl tr))
            (right-segment (make-segment br tr))
            (bottom-segment (make-segment bl br)))
        ((segments->painter (list left-segment top-segment right-segment bottom-segment)) frame)))))
```

2. This translates into drawing the line segments `(list (tl br) (bl tr))`
```scheme
(define frame-diagonal
  (lambda (frame)
    (let ((bl (make-vect 0 0))
          (tl (make-vect 0 1))
          (br (make-vect 1 0))
          (tr (make-vect 1 1)))
      (let ((forward-diag (make-segment bl tr))
            (backward-diag (make-segment tl br)))
        ((segments->painter (list forward-diag backward-diag)) frame)))))
```

3. This is similar to point 1 in that it contains four line segments. The difference is how the end points of a line segment are defined.
```scheme
(define frame-diamond
  (lambda (frame)
    (let ((mid-left (make-vect 0 0.5))
          (mid-top (make-vect 0.5 1))
          (mid-right (make-vect 1 0.5))
          (mid-bottom (make-vect 0.5 0)))
      (let ((top-left-segment (make-segment mid-left mid-top))
            (bottom-left-segment (make-segment mid-left mid-bottom))
            (top-right-segment (make-segment mid-top mid-right))
            (bottom-right-segment (make-segment mid-right mid-bottom)))
        ((segments->painter (list top-left-segment bottom-left-segment top-right-segment bottom-right-segment)) frame)))))
```

4. A smooth `wave` painter cannot be drawn using `draw-line` alone. Nevertheless, it can be approximated by drawing a series of very small line segments, each of which represents a distinct part of the picture.The smaller a segment, and the larger the number of these segments, the closer the line drawings get to the desired `wave`.