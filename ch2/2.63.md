Question
---
Each of the following two procedures converts a binary tree to a list.

```scheme
(define (tree->list-1 tree)
  (if (null? tree)
      '()
      (append (tree->list-1 (left-branch tree))
              (cons (entry tree)
                    (tree->list-1
                     (right-branch tree))))))

(define (tree->list-2 tree)
  (define (copy-to-list tree result-list)
    (if (null? tree)
        result-list
        (copy-to-list (left-branch tree)
                      (cons (entry tree)
                            (copy-to-list
                             (right-branch tree)
                             result-list)))))
  (copy-to-list tree '()))
```

a. Do the two procedures produce the same result for every tree? If not, how do the results differ? What lists do the two procedures produce for the trees in Figure 2.16

b. Do the two procedures have the same order of growth in the number of steps required to convert a balanced tree with $n$ elements to a list? If not, which one grows more slowly?

Solution
---
a. The procedures produce the same result for every tree, they produce `(1 3 5 7 9 11)` for the trees in Figure 2.16.

b. The procedure `tree->list-1` divides the tree in two halves, applies the procedure to each of the subtrees, and combines the result. The order of growth of the combination step is ${\Theta(n)}$ since appending two lists, each which has a size of $n/2$, takes $n$ steps. Thus, the order of growth of the procedure is given by the formula: ${T(n) = 2T(n/2) + n}$ which is ${\Theta(nlogn)}$.

The structure of `tree->list-2` is also the same as `tree->list-1` with the only difference being what is done when combining the results of the subtrees. It uses `cons`, which takes a constant time. The time complexity can be captured by the formul: ${T(n) = 2T(n/2) + c}$, where $c$ the cost of `cons`; therefore, $T(n)$ is ${\Theta(n)}$