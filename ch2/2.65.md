Question
---
Use the results of Exercise 2.63 and Exercise 2.64 to give $\Theta(n)$ implementations of `union-set` and `intersection-set` for sets implemented as (balanced) binary trees

Solution
---
Both procedures can be thought of as involving the following steps
1. Convert each of the input into ordered list using `tree->list-2`.
2. Apply the union/intersection of the two ordered list using the solution of Exercise 2.62 and `intersection-set` implementation from that section.
3. Convert the ordered list into a balanced tree using `list-tree` from Exercise 2.64.
Note that each of the above procedure has $\Theta(n)$ steps. 

```scheme
(define (union-set set1 set2)
  (define (union-ordered-list set1 set2)
    (cond ((null? set1) set2)
          ((null? set2) set1)
          (else (let ((x1 (car set1))
                      (x2 (car set2)))
                  (cond
                    ((= x1 x2) (cons x1 (union-ordered-list (cdr set1) (cdr set2))))
                    ((< x1 x2) (cons x1 (union-ordered-list (cdr set1) set2)))
                    ((< x2 x1) (cons x2 (union-ordered-list set1 (cdr set2)))))))))

  (cond ((null? set1) set2)
        ((null? set2) set1)
        (else (let ((x (tree->list set1))
                    (y (tree->list set2)))
                (list->tree (union-ordered-list x y))))))

(define (intersection-set set1 set2)
  (define (intersection-ordered-list set1 set2)
    (if (or (null? set1) (null? set2))
        '()
        (let ((x1 (car set1))
              (x2 (car set2)))
          (cond
            ((= x1 x2) (cons x1 (intersection-ordered-list (cdr set1) (cdr set2))))
            ((< x1 x2) (intersection-ordered-list (cdr set1) set2))
            ((< x2 x1) (intersection-ordered-list set1 (cdr set2)))))))

  (cond ((null? set1) '())
        ((null? set2) '())
        (else (let ((x (tree->list set1))
                    (y (tree->list set2)))
                (list->tree (intersection-ordered-list x y))))))
```