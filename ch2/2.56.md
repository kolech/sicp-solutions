Question
---
Show how to extend the basic differentiator to handle more kinds of expressions. For instance, implement the differentiation rule

$$
    {d(u^n) \over dx} = nu^{n-1}{du \over dx}
$$

by adding a new clause to the `deriv` program and defining appropriate procedures `exponentiation?`, `base`, `exponent`, and `make-exponentiation`. (You may use the symbol **
to denote exponentiation.) Build in the rules that anything
raised to the power 0 is 1 and anything raised to the power
1 is the thing itself.

Solution
---
```scheme
(define (deriv exp var)
  (cond ((number? exp) 0)
        ((variable? exp) (if (same-variable? exp var) 1 0))
        ((sum? exp) (make-sum (deriv (addend exp) var)
                              (deriv (augend exp) var)))
        ((product? exp) (make-sum (make-product (multiplier exp)
                                                (deriv (multiplicand exp) var))
                                  (make-product  (deriv (multiplier exp) var)
                                                 (multiplicand exp))))
        ((exponentiation? exp)
         (let ((u (base exp))
               (n (exponent exp)))
           (make-product n
                         (make-product (make-exponentiation u (- n 1))
                                       (deriv u var)))))
        (else (error "unknown expression type: -- DERIV" exp))))

(define (make-exponentiation b e)
  (cond ((= e 0) 1)
        ((= e 1) b)
        ((and (number? b) (number? e)) (expt b e))
        (else (list '** b e))))

(define (base exponentiation)
  (cadr exponentiation))

(define (exponent exponentiation)
  (caddr exponentiation))

(define (exponentiation? e)
  (and (pair? e) (eq? (car e) '**)))
```